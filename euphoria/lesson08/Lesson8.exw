include get.e
include dll.e
include machine.e
include msgbox.e
include constants.ew
include GLfunc.ew
include GLconst.ew

without warning

atom hRC, hDC, hWnd, hInstance, ClassName
sequence keys keys = repeat(0,256)  -- array to hold key presses

integer active, fullscreen, retval
active = TRUE
fullscreen = TRUE
hRC = NULL
hDC = NULL
hWnd = NULL
hInstance = NULL

integer light, blend, lp, fp, bp
light = 0
blend = 0
atom xrot, yrot, xspeed, yspeed, z
xrot = 0.0
yrot = 0.0
xspeed = 0.3
yspeed = 0.6
z = -5.0

atom LightAmbient, LightDiffuse, LightPosition
LightAmbient = allocate(16)
LightDiffuse = allocate(16)
LightPosition = allocate(16)
poke(LightAmbient,atom_to_float32(0.5)) poke(LightAmbient+4,atom_to_float32(0.5))
poke(LightAmbient+8,atom_to_float32(0.5)) poke(LightAmbient+12,atom_to_float32(1.0))
poke(LightDiffuse,atom_to_float32(1.0)) poke(LightDiffuse+4,atom_to_float32(1.0))
poke(LightDiffuse+8,atom_to_float32(1.0)) poke(LightDiffuse+12,atom_to_float32(1.0))
poke(LightPosition,atom_to_float32(0.0)) poke(LightPosition+4,atom_to_float32(0.0))
poke(LightPosition+8,atom_to_float32(2.0)) poke(LightPosition+12,atom_to_float32(1.0))

integer filter filter = 1
atom texture
texture = allocate(12)

integer dmScreenSettings, WindowRect

function NeHeLoadBitmap( sequence FileName,  atom texid)                    -- Creates Texture From A Bitmap File
atom  hBMP,                                                     -- Handle Of The Bitmap
         BMP,                                                       -- Bitmap Structure
         filename

    filename = allocate_string(FileName)
    BMP = allocate(24)
    c_proc(glGenTextures, {1, texid})                           -- Create The Texture
    hBMP= c_func(LoadImage, {c_func(GetModuleHandleA, {NULL}), filename, IMAGE_BITMAP, 0, 0, LR_CREATEDIBSECTION + LR_LOADFROMFILE} )

    if not hBMP then                                                            -- Does The Bitmap Exist?
        return FALSE                                                    -- If Not Return False
    end if

    retval = c_func(GetObjectA, {hBMP, 24, BMP})           -- Get The Object
                                                                -- hBMP:        Handle To Graphics Object
                                                                -- sizeof(BMP): Size Of Buffer For Object Information
                                                                -- &BMP:        Buffer For Object Information

    c_proc(glPixelStorei, {GL_UNPACK_ALIGNMENT, 4}) -- Pixel Storage Mode (Word Alignment / 4 Bytes)

    -- Typical Texture Generation Using Data From The Bitmap
    c_proc(glBindTexture, {GL_TEXTURE_2D, peek4u(texid)})                           -- Bind To The Texture ID
    c_proc(glTexParameteri, {GL_TEXTURE_2D, GL_TEXTURE_MIN_FILTER, GL_NEAREST})  -- Linear Min Filter
    c_proc(glTexParameteri, {GL_TEXTURE_2D, GL_TEXTURE_MAG_FILTER, GL_NEAREST})  -- Linear Mag Filter
    c_proc(glTexImage2D, {GL_TEXTURE_2D, 0, 3, peek4u(BMP+4), peek4u(BMP+8), 0, GL_BGR_EXT, GL_UNSIGNED_BYTE, peek4u(BMP+20)})
    c_proc(glBindTexture, {GL_TEXTURE_2D, peek4u(texid+4)})                           -- Bind To The Texture ID
    c_proc(glTexParameteri, {GL_TEXTURE_2D, GL_TEXTURE_MIN_FILTER, GL_LINEAR})  -- Linear Min Filter
    c_proc(glTexParameteri, {GL_TEXTURE_2D, GL_TEXTURE_MAG_FILTER, GL_LINEAR})  -- Linear Mag Filter
    c_proc(glTexImage2D, {GL_TEXTURE_2D, 0, 3, peek4u(BMP+4), peek4u(BMP+8), 0, GL_BGR_EXT, GL_UNSIGNED_BYTE, peek4u(BMP+20)})
    c_proc(glBindTexture, {GL_TEXTURE_2D, peek4u(texid+8)})                           -- Bind To The Texture ID
    c_proc(glTexParameteri, {GL_TEXTURE_2D, GL_TEXTURE_MIN_FILTER, GL_LINEAR})  -- Linear Min Filter
    c_proc(glTexParameteri, {GL_TEXTURE_2D, GL_TEXTURE_MAG_FILTER, GL_LINEAR_MIPMAP_NEAREST})  -- Linear Mag Filter
    c_proc(gluBuild2DMipmaps, {GL_TEXTURE_2D, 3, peek4u(BMP+4), peek4u(BMP+8), GL_RGB, GL_UNSIGNED_BYTE, peek4u(BMP+20)})

    retval = c_func(DeleteObject, {hBMP})                                                    -- Delete The Object

    return TRUE                                                        -- Loading Was Successful
end function

procedure ReSizeGLScene(integer width, integer height)
    if height = 0 then
        height = 1
    end if
    c_proc(glViewport,{0,0,width,height})
    c_proc(glMatrixMode,{GL_PROJECTION})
    c_proc(glLoadIdentity,{})
    c_proc(gluPerspective,{45.0,width/height,0.1,100.0})
    c_proc(glMatrixMode,{GL_MODELVIEW})
    c_proc(glLoadIdentity,{})
end procedure

procedure InitGL()
    if not NeHeLoadBitmap("Data\\Glass.bmp", texture) then
        printf(1,"Couldn't load texture\n",{})
    end if
    c_proc(glEnable,{GL_TEXTURE_2D})
    c_proc(glShadeModel,{GL_SMOOTH})
    c_proc(glClearColor,{0.0,0.0,0.0,0.5})
    c_proc(glClearDepth,{1.0})
    c_proc(glEnable,{GL_DEPTH_TEST})
    c_proc(glDepthFunc,{GL_LEQUAL})
    c_proc(glHint,{GL_PERSPECTIVE_CORRECTION_HINT,GL_NICEST})

    c_proc(glLightfv,{GL_LIGHT1,GL_AMBIENT,LightAmbient})
    c_proc(glLightfv,{GL_LIGHT1,GL_DIFFUSE,LightDiffuse})
    c_proc(glLightfv,{GL_LIGHT1,GL_POSITION,LightPosition})
    c_proc(glEnable,{GL_LIGHT1})

    c_proc(glColor4f, {1.0,1.0,1.0,0.5})
    c_proc(glBlendFunc, {GL_SRC_ALPHA,GL_ONE})
end procedure

function DrawGLScene()
    c_proc(glClear, {or_bits(GL_COLOR_BUFFER_BIT,GL_DEPTH_BUFFER_BIT)})
    c_proc(glLoadIdentity, {})
    c_proc(glTranslatef, {0.0,0.0,z})

    c_proc(glRotatef, {xrot,1.0,0.0,0.0})
    c_proc(glRotatef, {yrot,0.0,1.0,0.0})

    c_proc(glBindTexture,{GL_TEXTURE_2D,peek4u(texture+(filter-1)*4)})

    c_proc(glBegin, {GL_QUADS})

    c_proc(glNormal3f, {0.0,0.0,1.0})
    c_proc(glTexCoord2f,{0.0,0.0})  c_proc(glVertex3f, {-1.0,-1.0,1.0})
    c_proc(glTexCoord2f,{1.0,0.0})  c_proc(glVertex3f, {1.0,-1.0,1.0})
    c_proc(glTexCoord2f,{1.0,1.0})  c_proc(glVertex3f, {1.0,1.0,1.0})
    c_proc(glTexCoord2f,{0.0,1.0})  c_proc(glVertex3f, {-1.0,1.0,1.0})

    c_proc(glNormal3f, {0.0,0.0,-1.0})
    c_proc(glTexCoord2f,{1.0,0.0})  c_proc(glVertex3f, {-1.0,-1.0,-1.0})
    c_proc(glTexCoord2f,{1.0,1.0})  c_proc(glVertex3f, {-1.0,1.0,-1.0})
    c_proc(glTexCoord2f,{0.0,1.0})  c_proc(glVertex3f, {1.0,1.0,-1.0})
    c_proc(glTexCoord2f,{0.0,0.0})  c_proc(glVertex3f, {1.0,-1.0,-1.0})

    c_proc(glNormal3f, {0.0,1.0,0.0})
    c_proc(glTexCoord2f,{0.0,1.0})  c_proc(glVertex3f, {-1.0,1.0,-1.0})
    c_proc(glTexCoord2f,{0.0,0.0})  c_proc(glVertex3f, {-1.0,1.0,1.0})
    c_proc(glTexCoord2f,{1.0,0.0})  c_proc(glVertex3f, {1.0,1.0,1.0})
    c_proc(glTexCoord2f,{1.0,1.0})  c_proc(glVertex3f, {1.0,1.0,-1.0})

    c_proc(glNormal3f, {0.0,-1.0,0.0})
    c_proc(glTexCoord2f,{1.0,1.0})  c_proc(glVertex3f, {-1.0,-1.0,-1.0})
    c_proc(glTexCoord2f,{0.0,1.0})  c_proc(glVertex3f, {1.0,-1.0,-1.0})
    c_proc(glTexCoord2f,{0.0,0.0})  c_proc(glVertex3f, {1.0,-1.0,1.0})
    c_proc(glTexCoord2f,{1.0,0.0})  c_proc(glVertex3f, {-1.0,-1.0,1.0})

    c_proc(glNormal3f, {1.0,0.0,0.0})
    c_proc(glTexCoord2f,{1.0,0.0})  c_proc(glVertex3f, {1.0,-1.0,-1.0})
    c_proc(glTexCoord2f,{1.0,1.0})  c_proc(glVertex3f, {1.0,1.0,-1.0})
    c_proc(glTexCoord2f,{0.0,1.0})  c_proc(glVertex3f, {1.0,1.0,1.0})
    c_proc(glTexCoord2f,{0.0,0.0})  c_proc(glVertex3f, {1.0,-1.0,1.0})

    c_proc(glNormal3f, {-1.0,0.0,0.0})
    c_proc(glTexCoord2f,{0.0,0.0})  c_proc(glVertex3f, {-1.0,-1.0,-1.0})
    c_proc(glTexCoord2f,{1.0,0.0})  c_proc(glVertex3f, {-1.0,-1.0,1.0})
    c_proc(glTexCoord2f,{1.0,1.0})  c_proc(glVertex3f, {-1.0,1.0,1.0})
    c_proc(glTexCoord2f,{0.0,1.0})  c_proc(glVertex3f, {-1.0,1.0,-1.0})

    c_proc(glEnd, {})

    xrot += xspeed
    yrot += yspeed
    return TRUE
end function

procedure KillGLWindow()
    if fullscreen then
        if c_func(ChangeDisplaySettingsA,{NULL,0}) then end if
        if c_func(ShowCursor,{TRUE}) then end if
    end if
    if hRC then
        if c_func(wglMakeCurrent,{NULL,NULL}) then end if
        if c_func(wglDeleteContext,{hRC}) then end if
        hRC = NULL
    end if
    if hRC and not c_func(ReleaseDC,{hWnd,hDC}) then
        hDC = NULL
    end if
    if hWnd and not c_func(DestroyWindow,{hWnd}) then
        hWnd = NULL
    end if
    if dmScreenSettings then
        free(dmScreenSettings)
    end if
    free(WindowRect)
end procedure

function WndProc(atom hWnd, integer uMsg, atom wParam, atom lParam)
    if uMsg = WM_ACTIVATE then
        if not floor(wParam/#10000) then
            active = TRUE
        else
            active = FALSE
        end if
    elsif  uMsg = WM_SYSCOMMAND then
        if wParam = SC_SCREENSAVE then end if
        if wParam = SC_MONITORPOWER then end if
    elsif uMsg = WM_CLOSE then
        c_proc(PostQuitMessage,{0})
    elsif uMsg = WM_KEYDOWN then
        keys[wParam] = TRUE
    elsif uMsg = WM_KEYUP then
        keys[wParam] = FALSE
    elsif uMsg = WM_SIZE then
        ReSizeGLScene(and_bits(lParam,#FFFF),floor(lParam/#10000))
    end if
    return c_func(DefWindowProcA,{hWnd, uMsg, wParam, lParam})
end function

integer wc wc = allocate(40)
function ClassRegistration()
integer WndProcAddress, id
    id = routine_id("WndProc")
    if id = -1 then
    puts(1, "routine_id failed!\n")
    abort(1)
    end if
    WndProcAddress = call_back(id)
    hInstance = c_func(GetModuleHandleA,{NULL})
    ClassName = allocate_string("OpenGL")
    poke4(wc,or_all({CS_HREDRAW, CS_VREDRAW, CS_OWNDC}))
    poke4(wc+4,WndProcAddress)
    poke4(wc+8,0)
    poke4(wc+12,0)
    poke4(wc+16,hInstance)
    poke4(wc+20,c_func(LoadIconA,{NULL,IDI_WINLOGO}))
    poke4(wc+24,c_func(LoadCursorA,{NULL, IDC_ARROW}))
    poke4(wc+28,NULL)
    poke4(wc+32,NULL)
    poke4(wc+36,ClassName)
    if not c_func(RegisterClassA,{wc}) then
        retval = message_box("Failed to register class","Error", or_bits(MB_OK,MB_ICONINFORMATION))
        return FALSE
    else
        return TRUE
    end if
end function

integer regd regd = FALSE
procedure CreateGLWindow(atom title, integer width, integer height, integer bits, integer fullscreenflag)
    atom PixelFormat, pfd, dwExStyle, dwStyle
    sequence s
    if regd = FALSE then
        if ClassRegistration() then
            regd = TRUE
        end if
    end if
    fullscreen = fullscreenflag
    if fullscreen then
        dmScreenSettings = allocate(156)
        mem_set(dmScreenSettings,0,156)
        s = int_to_bytes(156)
        poke(dmScreenSettings + 36,{s[1],s[2]})
        poke4(dmScreenSettings + 40,or_all({DM_BITSPERPEL,DM_PELSWIDTH,DM_PELSHEIGHT}))
        poke4(dmScreenSettings + 104, bits)
        poke4(dmScreenSettings + 108, width)
        poke4(dmScreenSettings + 112, height)
        if c_func(ChangeDisplaySettingsA,{dmScreenSettings,CDS_FULLSCREEN}) != DISP_CHANGE_SUCCESSFUL then
            if message_box("The requested fullscreen mode is not supported by\nyour video card. " &
                           "Use windowed mode instead?","Error", or_bits(MB_YESNO,MB_ICONEXCLAMATION)) = IDYES then
            else
                retval = message_box("Program will now close","Error",or_bits(MB_OK,MB_ICONSTOP))
            end if
        end if
    else
        dmScreenSettings = NULL
    end if
    if fullscreen then
        dwExStyle = WS_EX_APPWINDOW
        dwStyle = WS_POPUP
        if c_func(ShowCursor,{FALSE}) then end if
    else
        dwExStyle = or_bits(WS_EX_APPWINDOW,WS_EX_WINDOWEDGE)
        dwStyle = WS_OVERLAPPEDWINDOW
    end if
    WindowRect = allocate(16)
    poke4(WindowRect,0)
    poke4(WindowRect + 4,width)
    poke4(WindowRect + 8, 0)
    poke4(WindowRect + 12, height)
    if c_func(AdjustWindowRectEx,{WindowRect, dwStyle, FALSE, dwExStyle}) then end if
    hWnd = c_func(CreateWindowExA,{dwExStyle,  --extended window style
                                   ClassName,  --class
                                   title,      --window caption
                                   or_all({WS_CLIPSIBLINGS,WS_CLIPCHILDREN,dwStyle}),  --window style
                                   0,
                                   0,
                                   peek4u(WindowRect + 4) - peek4u(WindowRect),
                                   peek4u(WindowRect + 12) - peek4u(WindowRect + 8),
                                   NULL,
                                   NULL,
                                   hInstance,
                                   NULL})
    if hWnd = NULL then
        KillGLWindow()
        retval = message_box("Window creation error","Error",or_bits(MB_OK,MB_ICONEXCLAMATION))
    end if
    pfd = allocate(40)  --PIXELFORMATDESCRIPTOR
    mem_set(pfd,0,40)
    poke(pfd, 40)  --size of pfd structure
    poke(pfd + 2, 1) --version
    poke4(pfd + 4, or_all({PFD_DRAW_TO_WINDOW,PFD_SUPPORT_OPENGL,PFD_DOUBLEBUFFER})) --properties flags
    poke(pfd + 8, PFD_TYPE_RGBA)  --request an rgba format
    poke(pfd + 9, 24)  --select color depth
    poke(pfd + 23, 24)  --32bit Z-buffer

    hDC = c_func(GetDC,{hWnd})  --create GL device context to match window device context
    if not hDC then
        KillGLWindow()
        retval = message_box("Can't create a GL device context","Error",or_bits(MB_OK,MB_ICONEXCLAMATION))
    end if
    PixelFormat = c_func(ChoosePixelFormat,{hDC,pfd})  --find a pixel format matching PIXELFORMATDESCRIPTOR
    if not PixelFormat then
        KillGLWindow()
        retval = message_box("Can't find a suitable pixel format","Error",or_bits(MB_OK,MB_ICONEXCLAMATION))
    end if
    if not (c_func(SetPixelFormat,{hDC,PixelFormat,pfd})) then  --set the pixel format
        KillGLWindow()
        retval = message_box("Can't set the pixel format","Error",or_bits(MB_OK,MB_ICONEXCLAMATION))
    end if
    if not c_func(DescribePixelFormat, {hDC,PixelFormat,40,pfd}) then
        retval = message_box("Can't describe the pixel format","Error",or_bits(MB_OK,MB_ICONEXCLAMATION))
    end if
    hRC = c_func(wglCreateContext,{hDC})  --create GL rendering context
    if not hRC then
        KillGLWindow()
        retval = message_box("Can't create a GL rendering context","Error",or_bits(MB_OK,MB_ICONEXCLAMATION))
    end if
    if not (c_func(wglMakeCurrent,{hDC,hRC})) then  --make the GL rendering context active
        KillGLWindow()
        retval = message_box("Can't activate the GL rendering context","Error",or_bits(MB_OK,MB_ICONEXCLAMATION))
    end if
    retval = c_func(ShowWindow,{hWnd,SW_SHOW}) --show the window
    retval = c_func(SetForegroundWindow,{hWnd}) --set it to always be in foreground
    retval = c_func(SetFocus,{hWnd}) --give it focus
    ReSizeGLScene(width, height)  --draw the GL scene to match the window size
    InitGL()  --initialize OpenGL
end procedure

integer MSG MSG = allocate(28)
integer title title = allocate_string("NeHe's Textures, Lighting and Keyboard Tutorial")
procedure WinMain()
integer done, msg_message
    done = FALSE
    if message_box("Would you like to run in fullscreen mode?","Start Fullscreen?",or_bits(MB_YESNO,MB_ICONQUESTION)) = IDNO then
        fullscreen = FALSE
    else
        fullscreen = TRUE
    end if
    CreateGLWindow(title,640,480,24,fullscreen)
    while not done do
        if c_func(PeekMessageA,{MSG,NULL,0,0,PM_REMOVE}) then
            msg_message = peek4u(MSG+4)
            if msg_message = WM_QUIT then
                done = TRUE
            else
                retval = c_func(TranslateMessage,{MSG})
                retval = c_func(DispatchMessageA,{MSG})
            end if
        else
            if ((active and not DrawGLScene()) or keys[VK_ESCAPE]) then
                done = TRUE
            else
                retval = c_func(SwapBuffers,{hDC})
                if keys['L'] and not lp then
                    lp = TRUE
                    if light then
                        light = 0
                    else
                        light = 1
                    end if
                    if not light then
                        c_proc(glDisable,{GL_LIGHTING})
                    else
                        c_proc(glEnable,{GL_LIGHTING})
                    end if
                end if
                if not keys['L'] then
                    lp = FALSE
                end if
                if keys['B'] and not bp then
                    bp = TRUE
                    if blend then
                        blend = 0
                    else
                        blend = 1
                    end if
                    if blend then
                        c_proc(glEnable, {GL_BLEND})
                        c_proc(glDisable, {GL_DEPTH_TEST})
                    else
                        c_proc(glDisable, {GL_BLEND})
                        c_proc(glEnable, {GL_DEPTH_TEST})
                    end if
                end if
                if not keys['B'] then
                    bp = FALSE
                end if
                if keys['F'] and not fp then
                    fp = TRUE
                    filter += 1
                    if filter > 3 then
                        filter = 1
                    end if
                end if
                if not keys['F'] then
                    fp = FALSE
                end if
                if keys[VK_PRIOR] then
                    z -= 0.02
                end if
                if keys[VK_UP] then
                    xspeed -= 0.01
                end if
                if keys[VK_DOWN] then
                    xspeed += 0.01
                end if
                if keys[VK_RIGHT] then
                    yspeed += 0.01
                end if
                if keys[VK_LEFT] then
                    yspeed -= 0.01
                end if
                if keys[VK_F1] then
                    keys[VK_F1] = FALSE
                    KillGLWindow()
                    if fullscreen = 0 then
                        fullscreen = 1
                    else
                        fullscreen = 0
                    end if
                    CreateGLWindow(title,640,480,24,fullscreen)
                end if
            end if
        end if
    end while
    KillGLWindow()
end procedure

WinMain()